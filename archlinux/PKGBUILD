# Maintainer: wszqkzqk <wszqkzqk@qq.com>

pkgname=pvz-portable
pkgver=0.1.1
pkgrel=1
pkgdesc="A cross-platform community-driven reimplementation of Plants vs. Zombies: Game of the Year Edition, aiming to bring the 100% authentic PvZ experience to every platform."
url="https://github.com/wszqkzqk/${pkgname}"
arch=('x86_64' 'aarch64' 'riscv64' 'loong64')
license=('LGPL-3.0-or-later')
depends=(
    libglvnd
    libjpeg-turbo
    libogg
    libopenmpt
    libpng
    libvorbis
    mpg123
    sdl2-compat
)
makedepends=(
    cmake
    git
    ninja
)
source=(
    "${pkgname}::git+file://${startdir}/.."
    # IMPORTANT: You must create this file yourself using assets from your legally owned copy.
    # See archlinux/README.md for instructions.
    "Plants_vs._Zombies_1.2.0.1073_EN.zip"
    "pvz-portable.sh"
    "io.github.wszqkzqk.pvz-portable.desktop"
)
sha256sums=('SKIP'
            '67e5aea21aa1cb7108ae9db1853692a432f1cceba921765e1081ea26479ef0f1'
            'b9dd103e31c17f867dad75af2bd901682f76e8a461e64a00b2538df2f2c584a8'
            '0a770062be1da2f5e8d6dc9514b951b70c8ab1b5c11fab470344cfd2c1990de5')

pkgver() {
    cd "${pkgname}"
    git describe --tags --abbrev=12 | sed 's/^v//;s/-/./g'
}

build() {
    cmake -B build -S "${pkgname}" \
        -DCMAKE_BUILD_TYPE='Release' \
        -DCMAKE_INSTALL_PREFIX='/usr'
    cmake --build build
}

package() {
    mkdir -p "${pkgdir}/usr/share/pvz-portable"
    install -Dm644 "main.pak" "${pkgdir}/usr/share/pvz-portable/main.pak"
    cp -r "properties" "${pkgdir}/usr/share/pvz-portable"

    install -Dm755 "build/pvz-portable" "${pkgdir}/usr/share/pvz-portable/pvz-portable"
    install -Dm755 "pvz-portable.sh" "${pkgdir}/usr/bin/pvz-portable"

    install -Dm644 "${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 "${pkgname}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    install -Dm644 "io.github.wszqkzqk.pvz-portable.desktop" "${pkgdir}/usr/share/applications/io.github.wszqkzqk.pvz-portable.desktop"
    install -Dm644 "${pkgname}/icon.png" "${pkgdir}/usr/share/pixmaps/io.github.wszqkzqk.pvz-portable.png"
}
